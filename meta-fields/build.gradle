plugins {
	id 'java'
	id("maven-publish")
	id("signing")
	id "com.gorylenko.gradle-git-properties" version "2.4.1"
	id("com.gradleup.nmcp").version("0.0.8")
}

java {
	sourceCompatibility = JavaVersion.VERSION_17
	targetCompatibility = JavaVersion.VERSION_17
}

repositories {
	mavenCentral()
}

dependencies {
	// https://mvnrepository.com/artifact/com.google.auto.service/auto-service
	annotationProcessor group: 'com.google.auto.service', name: 'auto-service', version: '1.1.1'
	compileOnly group: 'com.google.auto.service', name: 'auto-service', version: '1.1.1'

	testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.2'
	testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.2'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

	// https://mvnrepository.com/artifact/commons-io/commons-io
	testImplementation group: 'commons-io', name: 'commons-io', version: "2.15.0"
}

task javadocJar(type: Jar) {
	archiveClassifier = 'javadoc'
	from javadoc
}

task sourcesJar(type: Jar) {
	archiveClassifier = 'sources'
	from sourceSets.main.allSource
}

artifacts {
	archives javadocJar, sourcesJar
}

tasks.named('test') {
	useJUnitPlatform()
	// Set a temp directory as the working directory, because we are generating source files in the tests.
	def tempDir = File.createTempDir()
	tempDir.deleteOnExit()
	workingDir = tempDir
}

gitProperties {
	gitPropertiesName = "meta-fields-git.properties"
	keys = ['git.branch','git.build.version','git.commit.id','git.commit.time']
}
// make sure the generateGitProperties task always executes (even when git.properties is not changed)
generateGitProperties.outputs.upToDateWhen { false }

publishing {
	repositories {
		maven {
			name = "GitHubPackages"
			url = uri("https://maven.pkg.github.com/sc-moonlight/meta-fields")
			credentials {
				username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_TRIGGERING_ACTOR")
				password = project.findProperty("gpr.key") ?: System.getenv("GITHUB_TOKEN")
			}
		}
	}
	publications {
		sonatype(MavenPublication) {
			from components.java

			// Attach Javadoc and sources
			artifact sourcesJar
			artifact javadocJar
			pom {
				name = 'Meta-Fields'
				description = 'An annotation processor to create a safe way to reference object properties.'
				url = 'https://github.com/sc-moonlight/meta-fields'

				// SCM and other details can be customized here
				scm {
					connection = 'scm:git:https://github.com/sc-moonlight/meta-fields.git'
					developerConnection = 'scm:git:https://github.com/sc-moonlight/meta-fields.git'
					url = 'https://github.com/sc-moonlight/meta-fields'
				}

				licenses {
					license {
						name = 'The Apache License, Version 2.0'
						url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
					}
				}

				developers {
					developer {
						id = 'sc-moonlight'
						url = 'https://github.com/sc-moonlight/meta-fields'
					}
				}
			}
		}
	}
}

tasks.named('publishSonatypePublicationToGitHubPackagesRepository').configure {
	enabled = false
}
tasks.named('publishAllPublicationsToGitHubPackagesRepository').configure {
	enabled = false
}

signing {
	def signingKey = project.findProperty("signingKey")
	def signingPassword = project.findProperty("signingPassword")
	useInMemoryPgpKeys(signingKey, signingPassword)
	sign publishing.publications.sonatype
}

nmcp {
	publish("sonatype") {
		username = System.getenv("MAVEN_USERNAME")
		password = System.getenv("MAVEN_PASSWORD")
		publicationType = "USER_MANAGED"
	}
}